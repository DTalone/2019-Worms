
buildscript {
    ext.kotlin_version = '1.3.21'

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'idea'
    id 'maven'
}

group = 'za.co.entelect.challenge'


allprojects {
    repositories {
        mavenLocal()
        maven {
            url 'https://dl.bintray.com/entelect-challenge/game-engine'
        }
        jcenter()
    }
}

subprojects {
    version = '1.0'

    repositories {

    }

}

project(':game-engine-jvm') {
    apply plugin: 'kotlin'
    apply plugin: 'java'

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        implementation 'za.co.entelect.challenge:game-engine-interface:2019.0.0-RC0'

        testImplementation 'junit:junit:4.12'
        testImplementation 'org.mockito:mockito-core:2.+'
        testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0'

    }

    sourceSets {
        main.kotlin.srcDirs += '../game-engine-core/src/main'
        test.kotlin.srcDirs += '../game-engine-core/src/test'
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
}

project(':game-engine-web') {
    apply plugin: 'kotlin2js'

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-js"
    }

    sourceSets {
        main.kotlin.srcDirs += '../game-engine-core/src/main'
    }

    compileKotlin2Js {
        kotlinOptions.metaInfo = true
    }
}

