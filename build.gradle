import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import org.jetbrains.kotlin.gradle.dsl.KotlinCommonOptions
import org.jetbrains.kotlin.gradle.plugin.KotlinCompilation
import org.jetbrains.kotlin.gradle.plugin.KotlinTarget

import java.util.stream.Stream

plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.3.21'
    id 'com.github.johnrengelman.shadow' version '5.0.0'
    id 'jacoco'
}

repositories {
    mavenLocal()
    jcenter()
    maven {
        url 'https://dl.bintray.com/entelect-challenge/game-engine'
    }
}

kotlin {
    jvm()
    js()

    sourceSets {
        commonMain {
            kotlin.srcDir('game-engine-core/src/main')
            dependencies {
                implementation kotlin('stdlib-common')
            }
        }
        commonTest {
            kotlin.srcDir('game-engine-core/src/test')

            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }

        jvmMain {
            kotlin.srcDir('game-engine-jvm/src/main/kotlin')
            dependencies {
                implementation kotlin('stdlib-jdk8')
                api 'za.co.entelect.challenge:game-engine-interface:2019.0.0+'
            }
        }
        jvmTest {
            kotlin.srcDir('game-engine-jvm/src/test/kotlin')
            dependencies {
                implementation kotlin('test-junit')
                implementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0'
            }
        }

        jsMain {
            kotlin.srcDir('game-engine-web/src/main/kotlin')
            dependencies {
                implementation kotlin('stdlib-js')
            }

        }
        jsTest {
            kotlin.srcDir('game-engine-web/src/test/kotlin')
            dependencies {
                implementation kotlin('test-js')
            }
        }
    }
}

group = 'za.co.entelect.challenge'
version = '2019.0.0-RC0'

task jacocoTestReport(type: JacocoReport) {
    group 'report'
    description 'Generates a test coverage report'

    mustRunAfter jvmTest
    executionData jvmTest

    additionalClassDirs kotlinCompilation('jvm', 'main').output.classesDirs
    sourceDirectories('jvm', 'main').forEach {
        additionalSourceDirs it
    }

    reports {
        xml.enabled false
        csv.enabled false
    }
}

task jacocoTestCoverageVerification(type: JacocoCoverageVerification) {
    group 'verification'
    description 'Enforces test coverage on all classes'

    mustRunAfter jvmTest
    shouldRunAfter jacocoTestReport
    executionData jvmTest

    additionalClassDirs kotlinCompilation('jvm', 'main').output.classesDirs
    sourceDirectories('jvm', 'main').forEach {
        additionalSourceDirs it
    }
    
    violationRules {
        rule {
            element = 'CLASS'
            excludes = [
                    'za.co.entelect.challenge.game.delegate.*',
                    'za.co.entelect.challenge.game.engine.simplexNoise.*',

                    'za.co.entelect.challenge.game.engine.entities.GameConfig',

                    // Temporary exclusions until properly implemented:
                    'za.co.entelect.challenge.game.engine.WormsEngine',
                    'za.co.entelect.challenge.game.engine.processor.GameError'
            ]
            limit {
                minimum = 0.75
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification
check.dependsOn jacocoTestReport

task shadowJar(type: ShadowJar) {
    group = 'shadow'
    description = 'Creates a fat jar with all jvm classes and dependencies'

    from kotlinCompilation('jvm', 'main').output
    def runtimeClasspath = kotlinCompilation('jvm', 'main').runtimeDependencyFiles

    configurations = [runtimeClasspath]
    archiveAppendix.set('jvm-full')

    mustRunAfter check
}

build.dependsOn shadowJar

private Stream<FileCollection> sourceDirectories(String target, String compilation) {
    kotlinCompilation(target, compilation).allKotlinSourceSets.stream().map {
        it.kotlin.getSourceDirectories()
    }
}

private KotlinCompilation<KotlinCommonOptions> kotlinCompilation(String target, String compilation) {
    NamedDomainObjectCollection<KotlinTarget> targets = kotlin.targets
    targets.getByName(target).compilations.getByName(compilation)
}